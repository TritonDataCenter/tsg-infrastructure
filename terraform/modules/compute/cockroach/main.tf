locals {
  private_cns_domain = "${format("%s.%s", var.cns_service_tag, var.cns_fragment)}"
}

data "triton_account" "mod" {}

data "triton_datacenter" "mod" {}

data "template_file" "mod" {
  count = "${var.instance_count}"

  template = "${file(format("%s/templates/%s", path.module, "user_script.sh.tpl"))}"

  vars {
    data_center_name = "${data.triton_datacenter.mod.name}"
    consul_cns_url   = "${var.consul_cns_url}"
  }
}

resource "null_resource" "depends_on" {
  triggers {
    depends_on = "${join("", flatten(var.depends_on))}"
  }
}

resource "triton_machine" "mod" {
  count = "${var.instance_count}"

  name    = "${format("%s-cockroach-%02d", var.instance_name_prefix, count.index + 1)}"
  package = "${var.package}"
  image   = "${var.image}"

  root_authorized_keys = "${var.root_authorized_keys}"

  cloud_config = "${length(var.cloud_init_config) > 0 ?
                    element(concat(var.cloud_init_config, list("")),
                    count.index) : ""}"

  user_script = "${element(coalescelist(var.user_script,
                   data.template_file.mod.*.rendered),
                   count.index)}"

  firewall_enabled = "${var.firewall_enabled}"

  networks = [
    "${var.networks}",
  ]

  affinity = [
    "${format("instance!=~%s-cockroach-*", var.instance_name_prefix)}",
  ]

  cns {
    services = [
      "${var.cns_service_tag}",
    ]
  }

  metadata = "${var.metadata}"

  tags = "${merge(map(
    "name", "${format("%s-cockroach-%02d", var.instance_name_prefix, count.index + 1)}"
  ), var.tags)}"

  depends_on = [
    "null_resource.depends_on",
  ]

  lifecycle {
    ignore_changes = [
      "image",
      "tags",
    ]
  }
}

resource "random_shuffle" "mod" {
  input = [
    "${triton_machine.mod.*.primaryip}",
  ]

  result_count = 1
}

resource "null_resource" "mod" {
  count = "${var.instance_count}"

  triggers {
    cockroach_instance_ids = "${join(",", triton_machine.mod.*.id)}"
  }

  connection {
    type         = "ssh"
    user         = "ubuntu"
    host         = "${element(triton_machine.mod.*.primaryip, count.index)}"
    bastion_host = "${var.bastion_cns_url}"
    timeout      = "120s"
  }

  provisioner "file" {
    content = <<EOF
NODES='${join(",", sort(flatten(triton_machine.mod.*.ips)))}'
INSECURE='${var.insecure ? "true" : "false"}'
LEADER='${element(random_shuffle.mod.result, 0)}'
EOF

    destination = "/var/tmp/.cockroach-cluster"
  }

  provisioner "remote-exec" {
    scripts = [
      "${format("%s/files/%s", path.module, "configure.sh")}",
      "${format("%s/files/%s", path.module, "start.sh")}",
    ]
  }
}
